apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'nl.javadude.t2bus'
version = '1.2.1-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
	mavenCentral()
}

dependencies {
	compile "org.slf4j:slf4j-api:1.6.3"
	compile "com.google.guava:guava:13.0"
	testCompile "org.hamcrest:hamcrest-library:1.2.1"
	testCompile "org.hamcrest:hamcrest-core:1.2.1"
	testCompile "junit:junit-dep:4.10"
	testRuntime "ch.qos.logback:logback-classic:0.9.30"
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar, javadocJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	if(project.hasProperty('sonatypeUsername')) {
		repositories.mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			configuration = configurations.archives

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				name "Scannit"
				packaging "jar"
				description "An extensible Java Metadata/annotation scanner"
				url "https://github.com/hierynomus/scannit"
				inceptionYear "2011"

				scm {
					url "https://github.com/hierynomus/scannit/"
				}

				licenses {
					license {
						name "The Apache Software License, Version 2.0"
						url "http://www.apache.org/licenses/LICENSE-2.0.txt"
						distribution "repo"
					}
				}

				developers {
                    developer {
                        id "guava-team"
                        name "The Google Guava Team"
                    }
					developer {
						id "hierynomus"
						name "Jeroen van Erp"
						url "http://www.javadude.nl"
						roles {
							role "Developer"
						}
					}
				}
			}
		}
	}
}

